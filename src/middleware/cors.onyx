package nimbus

use core {tprintf}

CORS :: struct {
    allowed_origins: [] str
    max_age: u32
}

cors :: (origins: [] str, age := 86400) -> (c: CORS) {
    c.allowed_origins = origins
    c.max_age = age
    return
}

#local
cors_handler :: (c: &CORS, ctx: Context) {
    origins := str.join(c.allowed_origins, ", ") if c.allowed_origins else "*"

    ctx->set_header("Access-Control-Allow-Origin", origins)
    ctx->set_header("Access-Control-Allow-Methods", "POST, GET, OPTIONS")

    if ctx->method() == .OPTIONS {
        ctx->header("access-control-request-headers")
        |> Optional.value_or("")
        |> ctx->set_header("Access-Control-Allow-Headers", _)

        ctx->set_header("Access-Control-Max-Age", tprintf("{}", c.max_age))
        ctx->no_content(204)

    } else {
        ctx->next()
    }
}

#overload
ToMiddleware.convert :: macro (c: &CORS) => Middleware.{ cors_handler, c }

