package nimbus

use core.encoding.json
use core.os
use core.time
use core {tprintf}

Logger :: struct {
    writer: io.Writer

    style := LoggerStyle.V1
}

LoggerStyle :: enum {
    V1
    V2
    JSON
}


logger :: (s: &io.Stream = &core.stdio.stream, style := LoggerStyle.V1) -> Logger {
    return .{
        io.writer_make(s),
        style
    }
}

#local
logger_log_request :: (l: &Logger, c: Context) {
    start_time := os.time()
    c->next()
    duration   := os.time() - start_time

    req := c->request()
    res := c->_response()

    switch l.style {
        case .V1 do 
            io.write_format(
                &l.writer,
                "[{}] {} {} {}{} - {}\n",
                context.thread_id,
                req.address,
                req.method,
                get_header(&req.headers, "host") ?? "",
                req.endpoint,
                res.status)

        case .V2 {
            status_color := "92"
            if res.status / 100 != 2 {
                status_color = "93"
            }
            if res.status >= 400 {
                status_color = "91"
            }

            io.write_format(
                &l.writer,
                "{} \e[1;96m{} \e[1;{}m{}\e[0m \e[1;94m{} {}{} \e[90m{}\e[0m\n",
                time.gmtime(start_time / 1000)->format()
                "<1ms" if duration == 0 else tprintf("{}ms", duration)
                status_color,
                res.status,
                req.method,
                get_header(&req.headers, "host") ?? "",
                req.endpoint,
                req.address
            )
        }

        case .JSON {
            data := .{
                status = res.status
                method = tprintf("{}", req.method)
                endpoint = req.endpoint
                client = req.address
            }

            json.encode(&l.writer, data)
            l.writer->write("\n")
        }
    }

    io.writer_flush(&l.writer)
    io.stream_flush(l.writer.stream)
}

#overload
ToMiddleware.convert :: macro (l: &Logger) => Middleware.{ logger_log_request, l }
